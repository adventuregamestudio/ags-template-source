// label to use for text actions
Label* action;

// inventory GUI to use
GUI* interface_inv;

static function TwoClickHandler::RegisterInventoryGui(GUI* inventory_gui)
{
  interface_inv = inventory_gui;
}

static function TwoClickHandler::RegisterActionLabel(Label* label)
{
  action = label;
  action.Text = "";
}

//----------------------------------------------------------------------------------------------------
// on_mouse_click()
//----------------------------------------------------------------------------------------------------
function on_mouse_click(MouseButton button)
{
  // when mouse is clicked, text label is cleared
  if (action != null)
  {
    action.Text = "";
  }
  
  // Left Mouse Button on Object/Character/Hotspot/Location
  // when no inventory is selected:
  // - INTERACT with target
  // - walk to location
  // else
  // - USE inventory on target
  if (!IsGamePaused() && button == eMouseLeft)
  {
    if (GetLocationType(mouse.x, mouse.y) != eLocationNothing)
    {
      if (player.ActiveInventory == null)
      {
        Room.ProcessClick(mouse.x, mouse.y, eModeInteract);
      }
      else
      {
        Room.ProcessClick(mouse.x, mouse.y, eModeUseinv);
      }
    }
    else
    {
      if (player.ActiveInventory == null)
      {
        Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
      }
      else
      {
        player.ActiveInventory = null;
      }
    }       
  }

  // Right Mouse Button on Object/Character/Hotspot/Location
  // when no inventory is selected:
  // - EXAMINE target
  // else
  // - DESELECT inventory
  else if (!IsGamePaused() && button == eMouseRight)
  {
    if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
    else if (GetLocationType(mouse.x, mouse.y) != eLocationNothing)
    {
      Room.ProcessClick(mouse.x, mouse.y, eModeLookat);
    }
  }
  
  // Left Mouse Button on Inventory Item
  // when no inventory is selected:
  // - INTERACT with target 
  // - SELECT target
  // else
  // - USE inventory on target
  else if (button == eMouseLeftInv)
  {
    InventoryItem *i = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    if (i != null)
    {
      if (i.GetProperty("InstantUse") == true)
      {
        if (player.ActiveInventory == null)
        {
          i.RunInteraction(eModeInteract);
        }
        else
        {
          i.RunInteraction(eModeUseinv);
        }
      }
      else
      {
        if (player.ActiveInventory == null)
        {
          player.ActiveInventory = i;
        }
        else if (i.ID != player.ActiveInventory.ID)
        {
          i.RunInteraction(eModeUseinv);
        }
      }
    }
  }
  
  // Right Mouse Button on Inventory Item
  // when no inventory is selected:
  // - EXAMINE target
  // else
  // - DESELECT INVENTORY
  else if (button == eMouseRightInv)
  {
    if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
    else
    {
      InventoryItem *i = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
      if (i != null)
      {
        i.RunInteraction(eModeLookat);
      }
    }
  }
  
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute()
//----------------------------------------------------------------------------------------------------
function repeatedly_execute()
{
  // Inventory GUI:
  if (interface_inv == null)
  {
    // pass
  }
  else if (interface_inv.Visible && mouse.y > interface_inv.Height)
  {
    interface_inv.Visible = false;
  }
  else if (!IsGamePaused() && !interface_inv.Visible && mouse.y <= INVENTORY_POPUP_POSITION)
  {
    // make visible when the game is not paused and the cursor is within the popup position
    interface_inv.Visible = true;
  }
  
  // Action Text
  // We always display the name of what is under the mouse, with one exception:
  // IF the player has an inventory item selected and hovers over the same inventory item, 
  // we display nothing to indicate that an item can not be used on itself
  if (player.ActiveInventory == null)
  {
    if (action != null && !IsGamePaused())
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }
  else
  {
    InventoryItem *i = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    if (action == null)
    {
      // pass
    }
    else if (i != null && i.ID == player.ActiveInventory.ID)
    {
      action.Text = "";
    }
    else
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }
}
