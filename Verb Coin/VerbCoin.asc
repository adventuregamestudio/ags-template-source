// sprite for the GUI background
DynamicSprite* sprite;

// GUI to use
GUI* interface;

// default settings
int radius = VERBCOIN_DEFAULT_RADIUS;
int background_color = VERBCOIN_DEFAULT_BACKGROUND_COLOR;
int background_transparency = VERBCOIN_DEFAULT_BACKGROUND_TRANSPARENCY;
int border_color = VERBCOIN_DEFAULT_BORDER_COLOR;
int border_width = VERBCOIN_DEFAULT_BORDER_WIDTH;
int redraw = false;

// track where the interface was opened from
int context_x;
int context_y;
String context_text;

// map GUI controls against cursor modes
int modemap[];

// map GUI controls against text descrptions
String actionmap[];

// enable click handling and action label updates
bool enabled = false;

// whether buttons will be disabled if clicking them would result in an unhandled event
bool button_auto_disable = false;

function Clamp(static Maths, int value, int min, int max)
{
  if (value < min)
  {
    value = min;
  }
  
  if (value > max)
  {
    value = max;
  }
  
  return value;
}

function Min(static Maths, int value1, int value2)
{
  if (value1 > value2)
  {
    return value2;
  }
  
  return value1;
}

function set_buttons_enabled(bool state)
{
  for (int i; i < interface.ControlCount; i ++)
  {
    if (interface.Controls[i].AsButton != null)
    {
      interface.Controls[i].Enabled = state;
    }
  }
}

static function VerbCoin::SetRadius(int newradius)
{ 
  if (newradius < 1)
  {
    newradius = 1;
  }

  if (newradius != radius)
  {
    radius = newradius;
    redraw = true;
  }
}

static function VerbCoin::SetBackgroundTransparency(int transparency)
{
  transparency = Maths.Clamp(transparency, 0, 100);
  
  if (transparency != background_transparency)
  {
    background_transparency = transparency;
    redraw = true;
  }
}

static function VerbCoin::SetBackgroundColor(int color)
{
  color = Maths.Clamp(color, 0, 65535);
  
  if (color != background_color)
  {
    background_color = color;
    redraw = true;
  }
}

static function VerbCoin::SetBorderColor(int color)
{
  color = Maths.Clamp(color, 0, 65535);
  
  if (color != border_color)
  {
    border_color = color;
    redraw = true;
  }
}

static function VerbCoin::SetBorderWidth(int width)
{
  width = Maths.Clamp(width, 0, radius);
  
  if (width != border_width)
  {
    border_width = width;
    redraw = true;
  }
}

static function VerbCoin::OnClick(GUIControl* control, MouseButton button)
{
  interface.Visible = false;
  
  if (button == eMouseLeft || button == eMouseRight)
  {
    Room.ProcessClick(context_x, context_y, modemap[control.ID]);
  }
}

function place_button(GUIControl* control, VerbCoinPosition position)
{
  float edge;
  
  if (position == eVerbCoinPositionNorth || position == eVerbCoinPositionSouth)
  {
    edge = IntToFloat(control.Width) / 2.0;
  }
  else
  {
    edge = IntToFloat(control.Height) / 2.0;
  }
  
  float squared = Maths.RaiseToPower(IntToFloat(radius), 2.0) - Maths.RaiseToPower(edge, 2.0);
  
  if (squared < 0.0)
  {
    squared = 0.0;
  }
  
  float offset = Maths.Sqrt(squared);
  
  if (position == eVerbCoinPositionNorth)
  {
    control.X = radius - FloatToInt(edge);
    control.Y = radius - FloatToInt(offset, eRoundDown);
  }
  else if (position == eVerbCoinPositionEast)
  {
    control.X = radius + FloatToInt(offset, eRoundUp) - control.Width;
    control.Y = radius - FloatToInt(edge);
  }
  else if (position == eVerbCoinPositionSouth)
  {
    control.X = radius - FloatToInt(edge);
    control.Y = radius + FloatToInt(offset, eRoundUp) - control.Height;
  }
  else if (position == eVerbCoinPositionWest)
  {
    control.X = radius - FloatToInt(offset, eRoundDown);
    control.Y = radius - FloatToInt(edge);
  }
}

static function VerbCoin::RegisterButton(GUIControl* control, VerbCoinPosition position, CursorMode mode, String action)
{
  if (control.OwningGUI == interface && control.AsButton != null)
  {
    place_button(control, position);
    modemap[control.ID] = mode;
    actionmap[control.ID] = action;
    control.Visible = true;
  }
}

function render()
{
  // resize the GUI to fit the sprite
  int gui_size = radius * 2;
  gui_size = Maths.Min(gui_size, System.ViewportHeight);
  gui_size = Maths.Min(gui_size, System.ViewportWidth);
  gui_size ++;
  interface.Width = gui_size;
  interface.Height = gui_size;

  DynamicSprite* background = DynamicSprite.Create(interface.Width, interface.Height, true);
  DrawingSurface* surface;
  
  // redraw the sprite
  surface = background.GetDrawingSurface();
  surface.DrawingColor = border_color;
  surface.DrawCircle(radius, radius, radius);
  surface.DrawingColor = background_color;
  surface.DrawCircle(radius, radius, radius - border_width);
  surface.Release();

  sprite = DynamicSprite.Create(interface.Width, interface.Height, true);
  surface = sprite.GetDrawingSurface(); 
  surface.DrawImage(0, 0, background.Graphic, background_transparency);
  background.Delete();
  surface.Release();
  interface.BackgroundGraphic = sprite.Graphic;
}

static function VerbCoin::RegisterGui(GUI* interface_gui)
{
  interface = interface_gui;
  
  for (int i; i < interface.ControlCount; i ++)
  {
    if (interface.Controls[i].AsButton != null)
    {
      interface.Controls[i].Visible = false;
    }
  }
  
  modemap = new int[interface.ControlCount];  
  actionmap = new String[interface.ControlCount];
  enabled = true;
  render();
}

static function VerbCoin::Enable()
{
  enabled = true;
  set_buttons_enabled(true);
}

static function VerbCoin::Disable()
{
  enabled = false;
  set_buttons_enabled(false);
}

static function VerbCoin::IsEnabled()
{
  return enabled;
}

static function VerbCoin::Open()
{
  interface.Visible = true;
}

static function VerbCoin::Close()
{
  interface.Visible = false;
}

static function VerbCoin::IsOpen()
{
  return interface.Visible;
}

static function VerbCoin::ButtonAutoDisable(bool autodisable)
{
  button_auto_disable = autodisable;
}

function game_start()
{
  lblAction.Text = "";
}

function on_mouse_click(MouseButton button)
{
  if (interface == null || !enabled)
  {
    // don't do anything if GUI isn't set or is disabled
  }
  else if (button == eMouseLeft)
  {
    if (GetLocationType(mouse.x, mouse.y) != eLocationNothing)
    {
      if (player.ActiveInventory != null)
      {
        Room.ProcessClick(mouse.x, mouse.y, eModeUseinv);
      }
      else if (interface.Visible)
      {
        interface.Visible = false;
      }
      else if (gInventory.Visible)
      {
        gInventory.Visible = false;
      }
      else if (Character.GetAtScreenXY(mouse.x, mouse.y) != player)
      {
        context_x = mouse.x;
        context_y = mouse.y;
        interface.X = Maths.Clamp(context_x - radius, 0, System.ViewportWidth - interface.Width);
        interface.Y = Maths.Clamp(context_y - radius, 0, System.ViewportHeight - interface.Height);
        
        if (button_auto_disable)
        {
          for (int i; i < interface.ControlCount; i ++)
          {
            if (interface.Controls[i].AsButton != null)
            {
              interface.Controls[i].Enabled = IsInteractionAvailable(context_x, context_y, modemap[interface.Controls[i].ID]);
            }
          }
        }
        
        interface.Visible = true;
      }
    }
    // close windows or unset the active inventory item
    else if (interface.Visible)
    {
      interface.Visible = false;
    }
    else if (gInventory.Visible)
    {
      gInventory.Visible = false;
    }
    else if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
    else
    {
      // ...except when there is no nothing to deselect or close,
      // so just walk to this position
      Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
    }
  }
  else if (button == eMouseRight)
  {
    // close windows or unset the active inventory item
    if (interface.Visible)
    {
      interface.Visible = false;
    }
    else if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
    else if (gInventory.Visible)
    {
      gInventory.Visible = false;
    }
    else
    {
      // ...except when there is no nothing to deselect or close,
      // so a right click is also how the inventory is opened
      gInventory.Visible = true;
    }
  }
  else if (button == eMouseLeftInv)
  {
    InventoryItem* item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    
    if (player.ActiveInventory == null)
    {
      // left click to set active inventory
      player.ActiveInventory = item;
    }
    else if (item.ID != player.ActiveInventory.ID)
    {
      // left click to 'combine' items
      item.RunInteraction(eModeUseinv);
    }
    else
    {
      // clicking an item on itself closes the inventory window
      // (this is just a shortcut to avoid moving the cursor, as it means
      // you can just double click an item to also close the window)
      gInventory.Visible = false;
    }
  }
  else if (button == eMouseRightInv)
  {
    InventoryItem* item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    
    if (player.ActiveInventory == null && item != null)
    {
      // right click to look at item
      item.RunInteraction(eModeLookat);
    }
    else
    {
      // right click to deselect
      player.ActiveInventory = null;
    }
  }
}

function repeatedly_execute_always()
{
  if (redraw)
  {
    render();
    redraw = false;
  }
}

function repeatedly_execute()
{
  if (interface == null || !enabled)
  {
    // don't do anything if GUI isn't set or is disabled
  }
  else if (player.ActiveInventory == null)
  {
    if (interface.Visible)
    {
      // update text label for verb coin actions
      GUIControl* control = GUIControl.GetAtScreenXY(mouse.x, mouse.y);
      
      if (control != null && control.AsButton != null && control.Enabled)
      {
        lblAction.Text = String.Format("%s %s", actionmap[control.ID], context_text);
      }
      else
      {
        lblAction.Text = context_text;
      }
    }
    else if (!gInventory.Visible || GetLocationType(mouse.x, mouse.y) == eLocationNothing)
    {
      // update regular text label
      context_text = Game.GetLocationName(mouse.x, mouse.y);
      lblAction.Text = context_text;
    }
  }
  else
  {
    if (gInventory.Visible && GUI.GetAtScreenXY(mouse.x, mouse.y) != gInventory)
    {
      // close inventory window once the cursor leaves
      gInventory.Visible = false;
    }
    
    // update text label for 'combining' items
    String location = Game.GetLocationName(mouse.x, mouse.y);
    InventoryItem *i = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);

    if ((i != null && i.ID == player.ActiveInventory.ID) || location == "")
    {
      location = "...";
    }
    
    lblAction.Text = String.Format("Use %s with %s", player.ActiveInventory.Name, location);
  }
}

// handle clicks in the inventory area that are not on an inventory item
function on_event(EventType event, int data) 
{
  if (event == eEventGUIMouseDown &&
      data == gInventory.ID &&
      GUIControl.GetAtScreenXY(mouse.x, mouse.y) == invCustom &&
      InventoryItem.GetAtScreenXY(mouse.x, mouse.y) == null)
  {
    if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
    else
    {
      gInventory.Visible = false;
    }
  }
}