//===================================================================
// *** AGS MODULE SCRIPT ***
//
// Module:  Scumm VerbCoin GUI v1.7.1
//
// Author:  Tom "Electroshokker" Vandepoele
//
// 1 Abstract
// This script module implements a highly modifiable gui system similar to the one used 
// in the game "The Curse Of Monkey Island", also known as Monkey Island 3
//        
// 2 Revision History
// 2007-10-21 v1.0   		First Release, module is integrated in user template
// 2007-10-25 v1.1      Keyboard support added, some bugfixes, new verbcoin graphics by Misj'
// 2007-12-01 v1.2      Minor bugfixes and code cleanup
// 2007-12-01 v1.3      Doubleclick code added for 'Act' property
// 2007-12-16 v1.4      Minor bugfix - click in inventory executed look action at hotspots under inventory
// 2007-12-22 v1.5      Minor bugfix - click on empty spot in inventory and move over item caused item to be selected
// 2008-04-16 v1.5.1    Minor bugfix - resolution 800x600 now supported
// 2008-04-16 v1.5.2    Minor bugfix - walk-to function no longer gets blocked after clicking 
//                                     on something with property 'act' set to true
// 2008-04-16 v1.5.3    Minor bugfix - Code cleaned up so it's resolution independent. Bring on 1024x768, Chris! ;-)
// 2008-05-31 v1.5.4    Minor bugfix - Property action displayed in the @overhotspot@ label now shows correctly
//                                     when moving over the verbcoin gui (sometimes it displayed the action label of
//                                     the hotspot next to it
// 2008-06-14 v1.5.x    Many bugfixes
// 2008-06-15 v1.6.0    New feature - the entire module can be disabled with a simple function call
// 2008-07-19 v1.7.0    New feature - default action text 
//                      Bugfix - the correct custom action text is set when multiple instances exist on top of each other
// 2008-07-20 v1.7.1    New feature - disable inventory
// 2008-10-11 v1.7.2    Bugfix - large scrolling rooms now display verbcoin correctly
//
// 3 License
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to
// deal in the Software without restriction, including without limitation the
// rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
// sell copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
// THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
// DEALINGS IN THE SOFTWARE.
//
// Copyright (C) 2007-2008 Tom Vandepoele
//-------------------------------------------------------------------

//===================================================================
// Dynamic Variables
//-------------------------------------------------------------------
bool doubleclick;
bool timer_run;
bool obj_Act;
bool process;

int item_count;

int clicked; //if you've processed a click
int timer; //keeps the track of how long you press the button
int timer_click; //double-click or single click if timer passed

int mousex; //these are used to store the coordinates of where you clicked,
int mousey; //since the mouse coord. change while you select a button on the verbcoin

String location_name; //these are used to add to the hotspot/object/inv. item names
String action_name;		//in the "use", "pick up", "talk to" and "look at"
String standard_txt;						//when you move over a button

int guix; //these are used to figure out where you should put the GUI
int guiy; //when the place you clicked is too close to the edge of the screen

bool inv_entered,location_is_inv;	

//these allow you to activate the inventory while over the "exit borders"
//to the left and right of the inventory
//once you've moved over the borders (onto the inventory) they become active again

int chr_base;
int chr_base_y = 0;
int obj_base;
int obj_base_y = 0;

// these integers are used to determine whether the object or the character is on top

//-------------------------------------------------------------------
// SCUMM_VERBCOIN_GUI.setDefaultActions functions
//-------------------------------------------------------------------

bool usedefaultaction;
String default_look;
String default_talk;
String default_act;
String default_pickup;
String default_custom1;
String default_custom2;

//-------------------------------------------------------------------
// Mouse doubleclick speed
//-------------------------------------------------------------------

int dc_speed;

//-------------------------------------------------------------------
// Set the number of each button sprite
//-------------------------------------------------------------------

int verbgraphic_button0;    //idle verbcoin
int verbgraphic_button1;    //look at
int verbgraphic_button2;    //talk to
int verbgraphic_button3;    //interact with
int verbgraphic_button4;    //Usermode1 (custom_button1 graphics)
int verbgraphic_button5;    //Usermode2 (custom_button2 graphics)
int verbgraphic_button6;    //PickUp    (custom_button3 graphics)

//-------------------------------------------------------------------
// Set the gui (which gui is which)
//-------------------------------------------------------------------

int inv_underlay_id;        //underlay image for the inventory. needed only for alpha channel inventory items
int inv_id;                 //the actual inventory gui
int verbc_id;               //the verbcoin gui

//-------------------------------------------------------------------
// SCUMM_VERBCOIN_GUI.setInvBorders functions
//-------------------------------------------------------------------

bool x_borders_active = true;     //left and right borders active?
bool y_borders_active = false;    //up and down borders active?
int border_up;                    //exit_border position
int border_down;                  //exit_border position
int border_left;                  //exit_border position
int border_right;	                //exit_border position

//-------------------------------------------------------------------
// SCUMM_VERBCOIN_GUI.DisableVerbCoinGUI
//-------------------------------------------------------------------

bool module_disabled = false;

//-------------------------------------------------------------------
// SCUMM_VERBCOIN_GUI.DisableInventory
//-------------------------------------------------------------------

bool inventory_disabled = false;

//===================================================================
// Module-only variables
//-------------------------------------------------------------------

//-------------------------------------------------------------------
//	@overhotspot@ label text customization
//-------------------------------------------------------------------

String custom_1;	//look at	
String custom_2;	//talk to
String custom_3;	//interact with
String custom_4;	//Usermode1
String custom_5;	//Usermode2
String custom_6;	//PickUp 

//-------------------------------------------------------------------
//	Game resolution auto-correct
//-------------------------------------------------------------------

int resolution_y;
int verbc_border_x;
int verbc_border_y;

//===================================================================
// Export Functions to be called in the Global Script
//------------------------------------------------------------------- 

static function SCUMM_VERBCOIN_GUI::DoubleClickSpeed(int speed) {
  dc_speed = speed;
}

static function SCUMM_VERBCOIN_GUI::UseDefaultAction(bool option){
    usedefaultaction = option;
}

static function SCUMM_VERBCOIN_GUI::SetDefaultAction(DefaultAction option,  String action) {
  if (option == aLook){
    default_look = action;
  }
  if (option == aTalk){
    default_talk = action;
  }
  if (option == aInteract){
    default_act = action;
  }
  if (option == aCustom1){
    default_pickup = action;
  }
  if (option == aCustom2){
    default_custom1 = action;
  }
  if (option == aCustom3){
    default_custom2 = action;
  }
}

static function SCUMM_VERBCOIN_GUI::Inv_Border_active(bool x_borders,bool y_borders) {
  x_borders_active = x_borders;
  y_borders_active = y_borders;
}

static function SCUMM_VERBCOIN_GUI::Inv_Border_SetPos(int top, int bottom, int left, int right) {
  border_up = top;
  border_down = bottom;
  border_left = left;
  border_right = right;
}

static function SCUMM_VERBCOIN_GUI::Item_Count(int count) {
  item_count = count;
}

static function SCUMM_VERBCOIN_GUI::InvScroll_Right() {
  if(Inv_window.ItemAtIndex[Inv_window.TopItem + item_count] != null){
    Inv_window.TopItem = Inv_window.TopItem + item_count;
    btnleft.Visible = true;
    if(Inv_window.ItemAtIndex[Inv_window.TopItem + item_count] == null){
      btnright.Visible = false;
    }
  }
}

static function SCUMM_VERBCOIN_GUI::InvScroll_Left() {
  if(Inv_window.ItemAtIndex[Inv_window.TopItem - item_count] != null){
    Inv_window.TopItem = Inv_window.TopItem - item_count;
    btnright.Visible = true;
    if(Inv_window.ItemAtIndex[Inv_window.TopItem - item_count] == null){
      btnleft.Visible = false;
    }
  }
}

static function SCUMM_VERBCOIN_GUI::verbgraphic(ButtonChoice option,  int sprite_number) {
  if (option == bIdle){ 
    verbgraphic_button0 = sprite_number;
  }
  if (option == bLook){
    verbgraphic_button1 = sprite_number;
  }
  if (option == bTalk){
    verbgraphic_button2 = sprite_number;
  }
  if (option == bInteract){
    verbgraphic_button3 = sprite_number;
  }
  if (option == bCustom1){
    verbgraphic_button4 = sprite_number;
  }
  if (option == bCustom2){
    verbgraphic_button5 = sprite_number;
  }
  if (option == bCustom3){
    verbgraphic_button6 = sprite_number;
  }
}

static function SCUMM_VERBCOIN_GUI::Inventory_GUI(int gInventory_ID, int gInvUnderlay_ID) {
  inv_underlay_id =  gInvUnderlay_ID;
  inv_id = gInventory_ID;
}

static function SCUMM_VERBCOIN_GUI::Verbcoin_GUI(int gVerbcoin_ID) {
  verbc_id = gVerbcoin_ID;
}

static function SCUMM_VERBCOIN_GUI::DisableInventory(bool value){
  inventory_disabled = value;
}

static function SCUMM_VERBCOIN_GUI::GoInventory() {
  if(inventory_disabled == false){
  
  if(gui[inv_id].Visible == true){
    gui[inv_id].Visible = false;
    gui[inv_underlay_id].Visible = false;
  }
  else{
    inv_entered = true;  
    gui[inv_id].Visible = true;
    gui[inv_underlay_id].Visible = true;
    if (Inv_window.ItemAtIndex[Inv_window.TopItem + item_count] != null){
      btnright.Visible = true;
		}
		else{
      btnright.Visible = false;
		}
		if(Inv_window.ItemAtIndex[Inv_window.TopItem - item_count] != null){
			btnleft.Visible = true;
		}
		else{
      btnleft.Visible = false;
		}
  }
  }
}

static function SCUMM_VERBCOIN_GUI::Select() {
  if (InventoryItem.GetAtScreenXY(mouse.x, mouse.y) != null){
    if (player.ActiveInventory == null){
      player.ActiveInventory = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    }
    else{
      InventoryItem *item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
      item.RunInteraction(eModeUseinv);
    }
  }
  else{
      ProcessClick(mouse.x, mouse.y, eModeUseinv );
  }
}

static function SCUMM_VERBCOIN_GUI::Deselect() {
  if (mouse.Mode == eModeUseinv){
    player.ActiveInventory = null;
  }
  else{
    if(gui[inv_id].Visible == true){
      gui[inv_id].Visible = false;
      gui[inv_underlay_id].Visible = false;
    }
  }
}

static function SCUMM_VERBCOIN_GUI::RunInteraction(CursorMode option){
  if (option == eModePointer || option == eModeUseinv || option == eModeWait || option == eModeWalkto){
    //do nothing
  }
  else{
    if (mouse.Mode == eModeUseinv){   //deselect the item first
      player.ActiveInventory = null;
    }
    if (gInventory.Visible == true){
      if (InventoryItem.GetAtScreenXY(mouse.x, mouse.y) != null){
      InventoryItem *item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
      item.RunInteraction(option); //interact with item
      }
    }
    else{
      ProcessClick(mouse.x, mouse.y, eModeInteract);
    }
  }
}

static function SCUMM_VERBCOIN_GUI::DisableVerbCoinGUI(bool value){
  module_disabled = value;
}

//===================================================================
// Start of module code
//===================================================================

// The "Popup modal" option:
// With this option, the game will be paused while the GUI is displayed, 
// during which time the on_mouse_click and on_key_press functions will not get run.
//
// As a result, I need the inventory and it's underlay to be NORMAL gui's, so the on_key_press functions will work!
// However, normal guis are always visible on a room start, so I hide them before entering any room (beforefadein)

//===================================================================
// Verbcoin re-positioning
//-------------------------------------------------------------------

function on_event (int event, int data) {
  
  if (module_disabled == false){
  //*****************************************************************  

  if (event==eEventEnterRoomBeforeFadein){
    gui[inv_id].Visible = false;
    gui[inv_underlay_id].Visible = false;
    verbc_border_x = system.viewport_width - gui[verbc_id].Width;
    verbc_border_y = system.viewport_height - gui[verbc_id].Height;
  }

//===================================================================
// CLICK MOUSE BUTTON (IN INVENTORY)
//-------------------------------------------------------------------
  if (event==eEventGUIMouseDown && data == gui[inv_id].ID)  { // when you are in your inventory
    if (mouse.IsButtonDown(eMouseRight)) {
      if (mouse.Mode == eModeUseinv){
				player.ActiveInventory = null;
			}
			else{
				gui[inv_id].Visible = false;
				gui[inv_underlay_id].Visible = false;
			}
		} 
		else if (mouse.IsButtonDown(eMouseLeft)) {
			if (player.ActiveInventory == null && GUIControl.GetAtScreenXY(mouse.x,  mouse.y) == Inv_window){ 
					clicked = 1;			//we trigger the calculating
					mousex = mouse.x; //we set these params to store the exact location of
					mousey = mouse.y; //where you clicked
				
					guix = mousex - gui[verbc_id].Width/2; //we set the location of the GUI
					guiy = mousey - gui[verbc_id].Height/2; //and center it
    
					// if you're verbcoin is of a different size then 120x120 pixels, change these values. 
				
					if (guix < 0) guix = 0; if (guix > verbc_border_x) guix = verbc_border_x;
					if (guiy < 0) guiy = 0; if (guiy > verbc_border_y) guiy = verbc_border_y;
					// if you click too close to the screen edge, this code sets your verbcoin away from the edge
			}
			else{	// if you are holding an active item and there's an item under your cursor
          if (InventoryItem.GetAtScreenXY(mouse.x, mouse.y) != null){
            InventoryItem *item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
            item.RunInteraction(eModeUseinv);
          }
			}
		}
  }
  //*****************************************************************
  }
}

function on_mouse_click(int button) {
  
  if (module_disabled == false){
  //*****************************************************************
  
//===================================================================
// CLICK MOUSE BUTTON (OUTSIDE INVENTORY)
//-------------------------------------------------------------------
	/*RIGHT-CLICK*/
	if (button==eMouseRight) {
		if (gui[inv_id].Visible == false){			// if inventory not active
			if (mouse.Mode == eModeWalkto && inventory_disabled == false){	// and you're not holding an active item or something
				if (Inv_window.ItemAtIndex[Inv_window.TopItem + item_count] != null){
					btnright.Visible = true;
				}
				else{
					btnright.Visible = false;
				}
				if(Inv_window.ItemAtIndex[Inv_window.TopItem - item_count] != null){
					btnleft.Visible = true;
				}
				else{
					btnleft.Visible = false;
				}
				player.ActiveInventory = null;
				inv_entered = true;
        gui[inv_id].Visible = true;					// activate the inventory
				gui[inv_underlay_id].Visible = true;
			}
			else{
				player.ActiveInventory = null;
				overhotspot.Text = "@overhotspot@";
			}
		}
	}
	/*LEFT-CLICK*/
  else if (button==eMouseLeft) {
    if (timer_run == true){
      doubleclick = true;
    }
    else{
      doubleclick = false;
      timer_run = true;
    }
			clicked = 1; //we trigger the calculating
			if (gui[inv_id].Visible == false){
				location_name = Game.GetLocationName(mouse.x, mouse.y); //get the name of whatever you clicked on
			}
				mousex = mouse.x; //we set these params to store the exact location of
				mousey = mouse.y; //where you clicked

				guix = mousex - gui[verbc_id].Width/2; //we set the location of the GUI
				guiy = mousey - gui[verbc_id].Height/2; //and center it

				if (guix < 0) guix = 0; if (guix > verbc_border_x) guix = verbc_border_x;
				if (guiy < 0) guiy = 0; if (guiy > verbc_border_y) guiy = verbc_border_y;
    
				// if you click too close to the screen edge, this code sets your verbcoin away from the edge
	}
  //*****************************************************************  
  }
}

//===================================================================
function repeatedly_execute() {   
//===================================================================

Object*obj=Object.GetAtScreenXY(mouse.x, mouse.y);
Hotspot*htspt=Hotspot.GetAtScreenXY(mouse.x, mouse.y);
Character*chr=Character.GetAtScreenXY(mouse.x, mouse.y);
InventoryItem*item=InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
InventoryItem *item2; //(special check)
GUIControl *control = GUIControl.GetAtScreenXY(mouse.x, mouse.y);
GUI *guixy = GUI.GetAtScreenXY(mouse.x, mouse.y);

  if (module_disabled == false){
  //***************************************************************** 

if (timer_run == true){
  timer_click++;
  if (timer_click == dc_speed){
    timer_click = 0;
    timer_run = false;
    if (process == true){
      process = false;
      ProcessClick(mousex,mousey, eModeInteract);
      mouse.Mode = eModeWalkto;
    }
  }
}

//===================================================================
// INVENTORY EXIT BORDERS
//-------------------------------------------------------------------

if (gui[inv_id].Visible == true){ 				//The inventory is active
	if(inv_entered == false){
		if(mouse.x < border_left || mouse.x > border_right){
			if(x_borders_active == true){
					gui[inv_id].Visible = false;					
					gui[inv_underlay_id].Visible = false;
					if (mouse.Mode != eModeUseinv){ 
						mouse.Mode = eModeWalkto;
						player.ActiveInventory = null; 
					}
			}
		}
		if(mouse.y < border_up || mouse.y > border_down){
			if(y_borders_active == true){
				gui[inv_id].Visible = false;					
				gui[inv_underlay_id].Visible = false;		
				if (mouse.Mode != eModeUseinv){ 
					mouse.Mode = eModeWalkto;
					player.ActiveInventory = null; 
				}
			}
		}
	}
	if(mouse.x > border_left && mouse.x < border_right && mouse.y > border_up && mouse.y < border_down){
		if(inv_entered == true){	//if you opened the inventory while over the exit borders,
      inv_entered = false;		//the exit borders become active again
    }
  }
}

//===================================================================
// IMPORT PROPERTIES 
//-------------------------------------------------------------------

if (gui[verbc_id].Visible == false) {
	if (GetLocationType(mouse.x, mouse.y) != eLocationNothing){
    location_name = Game.GetLocationName(mouse.x, mouse.y);
    if (player.ActiveInventory != null){											// Have an active inventory item?
        action_name = player.ActiveInventory.Name;
				standard_txt = String.Format("Use %s on %s",action_name,location_name); 
				overhotspot.Text = standard_txt;		
		}
    else{
      if (GetLocationType(mouse.x,mouse.y) == eLocationCharacter){
        custom_1 = chr.GetTextProperty("Custom_Look");
        custom_2 = chr.GetTextProperty("Custom_Talk");
        custom_3 = chr.GetTextProperty("Custom_Interact");
        custom_4 = chr.GetTextProperty("Custom_Button1");
        custom_5 = chr.GetTextProperty("Custom_Button2");
        custom_6 = chr.GetTextProperty("Custom_Button3");
      }
      else if (GetLocationType(mouse.x,mouse.y) == eLocationObject){
        custom_1 = obj.GetTextProperty("Custom_Look");
        custom_2 = obj.GetTextProperty("Custom_Talk");
        custom_3 = obj.GetTextProperty("Custom_Interact");
        custom_4 = obj.GetTextProperty("Custom_Button1");
        custom_5 = obj.GetTextProperty("Custom_Button2");
        custom_6 = obj.GetTextProperty("Custom_Button3");
      }
      else if (GetLocationType(mouse.x,mouse.y) == eLocationHotspot){
        custom_1 = htspt.GetTextProperty("Custom_Look");
        custom_2 = htspt.GetTextProperty("Custom_Talk");
        custom_3 = htspt.GetTextProperty("Custom_Interact");
        custom_4 = htspt.GetTextProperty("Custom_Button1");
        custom_5 = htspt.GetTextProperty("Custom_Button2");
        custom_6 = htspt.GetTextProperty("Custom_Button3");
      }
    }
  }
  else{
      overhotspot.Text = "@overhotspot@";
  }
	if(gui[inv_id].Visible == true){
		if(item != null){    //custom properties inventory item
		  if (player.ActiveInventory == null){			
				custom_1 = item.GetTextProperty("Custom_Look");
				custom_2 = item.GetTextProperty("Custom_Talk");
				custom_3 = item.GetTextProperty("Custom_Interact");
        custom_4 = item.GetTextProperty("Custom_Button1");
        custom_5 = item.GetTextProperty("Custom_Button2");
        custom_6 = item.GetTextProperty("Custom_Button3");
			}
			else{
				location_name = Game.GetLocationName(mouse.x, mouse.y);
				action_name = player.ActiveInventory.Name;
				if(location_name != null){
					if (location_name != action_name){
						standard_txt = String.Format("Use %s on %s",action_name,location_name); 
						overhotspot.Text = standard_txt;
					}
					else{
						overhotspot.Text = "@overhotspot@";	//if something else, just display standard
					}
				}
			}
    }
    else{
      overhotspot.Text = "@overhotspot@";
    }
	}

//===================================================================
// LONG/SHORT MOUSE CLICK (VERBCOIN IS NOT VISIBLE)
//-------------------------------------------------------------------

	if (clicked == 1) {
    //-------------------------------------------------------------------
    // Check if the object/hotspot/character/item under the mouse has property "act" set to true
    //-------------------------------------------------------------------
      process = false;
      
      if(GetLocationType(mouse.x, mouse.y) == eLocationObject){
        if (obj.GetProperty("act") == true){
          process = true;
        }
      }
      if(GetLocationType(mouse.x, mouse.y) == eLocationHotspot){
        if (htspt.GetProperty("act") == true){
          process = true;
        }
      }
      if(GetLocationType(mouse.x, mouse.y) == eLocationCharacter){
        if (chr.GetProperty("act") == true){
          process = true;
        }
      }
      if(item != null){
        if (item.GetProperty("act") == true){
          process = true;
        }
      }
    //-------------------------------------------------------------------
    
		if (Mouse.Mode != eModeUseinv && process == false){
			if (Mouse.IsButtonDown(eMouseLeft) == 1) timer++;
        /*LONG CLICK*/
      if (timer >= 15) { 	
        if (GetLocationType(mouse.x, mouse.y) != eLocationNothing || item != null){
				// if there's a hotspot, inventory item OR an object at the mouse position, THEN pop up the action gui
				// this prevents the game from crashing when it doesn't find anything (when it returns a nullpointer)
          if (item != null){
					//this stuff makes sure you see the right names pop up in your text (@overhotspot@) gui
            player.ActiveInventory = item;
            location_name = player.ActiveInventory.Name;
            player.ActiveInventory = null;
            location_is_inv = true;
          }
          else{
            location_is_inv = false;
          }
          // ok, now activate the gui
          gui[verbc_id].SetPosition(guix, guiy);	// put the verbcompass on the right spot
          gui[verbc_id].BackgroundGraphic = verbgraphic_button0;  
          gui[verbc_id].Visible = true;						// activate
        } 					
        clicked=0; 	//click done
        timer=0;		//timer reset
      }
      }
      if (Mouse.IsButtonDown(eMouseLeft) == 0){ //this executes if the button is released
        /*SHORT CLICK*/ 
          if (mouse.Mode != eModeUseinv){	//not holding an item
            if (gui[inv_id].Visible==true){
              if (item != null){
                player.ActiveInventory = item;
                action_name = player.ActiveInventory.Name;
              }          
            }
          else{
              if (process == false){
                ProcessClick(mousex,mousey, eModeWalkto);  //move to that spot 
              }
            }
          }
          else{			
            ProcessClick(mousex,mousey, eModeUseinv); //inventory interact
          }
        clicked=0; 	//click done
        timer=0;		//timer reset
      }
    }
  }
//===================================================================
// @OVERHOTSPOT@ LABEL GUI when over verbcoin button
//-------------------------------------------------------------------

  else { 
		/*LOOK ACTION*/
			if (control == look_button){
				gui[verbc_id].BackgroundGraphic = verbgraphic_button1;
        if (custom_1 == "" && usedefaultaction == true && default_look != null){
          overhotspot.Text = default_look.Append(location_name);
        }
        else{
          overhotspot.Text = custom_1;
        }
			}
		/*TALK_ACTION*/
			if (control == talk_button){ 
				gui[verbc_id].BackgroundGraphic = verbgraphic_button2;
        if (custom_2 == "" && usedefaultaction == true && default_talk != null){
          overhotspot.Text = default_talk.Append(location_name);
        }
        else{
          overhotspot.Text = custom_2;
        }
			}
		/*INTERACT ACTION*/
			if (control == interact_button){
				gui[verbc_id].BackgroundGraphic = verbgraphic_button3; 
        if (custom_3 == "" && usedefaultaction == true && default_act != null){
          overhotspot.Text = default_act.Append(location_name);
        }
        else{
          overhotspot.Text = custom_3;
        }
			}
		/*USERMODE1 ACTION*/
			if (control == custom_button1){
				gui[verbc_id].BackgroundGraphic = verbgraphic_button4;
        if (custom_4 == "" && usedefaultaction == true && default_pickup != null){
          overhotspot.Text = default_pickup.Append(location_name);
        }
        else{
          overhotspot.Text = custom_4;
        }
			}
		/*USERMODE2 ACTION*/
			if (control == custom_button2){
				gui[verbc_id].BackgroundGraphic = verbgraphic_button5;
        if (custom_5 == "" && usedefaultaction == true && default_custom1 != null){
          overhotspot.Text = default_custom1.Append(location_name);
        }
        else{
          overhotspot.Text = custom_5;
        }
			}
		/*PICKUP ACTION*/
			if (control == custom_button3){
				gui[verbc_id].BackgroundGraphic = verbgraphic_button6;
        if (custom_6 == "" && usedefaultaction == true && default_custom2 != null){
          overhotspot.Text = default_custom2.Append(location_name);
        }
        else{
          overhotspot.Text = custom_6;
        }
			}
		if (guixy != gui[verbc_id]){
		  gui[verbc_id].Visible = false;
		  overhotspot.Text = "@overhotspot@";
			Wait(1);
		}
		
//===================================================================
// PROCESS VERBCOIN BUTTON CLICKS - OUTSIDE INVENTORY
//-------------------------------------------------------------------
					
		if (Mouse.IsButtonDown(eMouseLeft) == 0){		// leftmousebutton released?
			overhotspot.Text = "@overhotspot@";
			/*inventory is off*/
			if (gui[inv_id].Visible == false){	
				if (guixy == gui[verbc_id]){
				  if (gui[verbc_id].BackgroundGraphic == verbgraphic_button1){	//look button graphics shown
						gui[verbc_id].Visible = false;
						Wait(1); 
						ProcessClick(mousex,mousey,eModeLookat);
						}
					if (gui[verbc_id].BackgroundGraphic == verbgraphic_button2){	//talk to button graphics shown
						gui[verbc_id].Visible = false; 
						Wait(1); 																		
						ProcessClick(mousex,mousey,eModeTalkto);		
						}
					if (gui[verbc_id].BackgroundGraphic == verbgraphic_button3){	//interact button graphics shown
						gui[verbc_id].Visible = false;	
						Wait(1);
						ProcessClick(mousex,mousey,eModeInteract);
						}
					if (gui[verbc_id].BackgroundGraphic == verbgraphic_button4){	//custom button1 graphics shown
						gui[verbc_id].Visible = false;	
						Wait(1);
						ProcessClick(mousex,mousey,eModeUsermode1);
					}
					if (gui[verbc_id].BackgroundGraphic == verbgraphic_button5){	//custom button2 graphics shown
            gui[verbc_id].Visible = false;
						Wait(1);
						ProcessClick(mousex,mousey,eModeUsermode2);
					}
					if (gui[verbc_id].BackgroundGraphic == verbgraphic_button6){	//custom button3 graphics shown
						gui[verbc_id].Visible = false;	
						Wait(1);
						ProcessClick(mousex,mousey,eModePickup);
          }
				}
      }
    
//===================================================================
// PROCESS VERBCOIN BUTTON CLICKS - INVENTORY
//-------------------------------------------------------------------
// 
// Note: for each action the inventory item that is below the verbcoin should be used
// since the x- and y-coördinates are no longer valid, we use the saved mousex and mousey coördinates
// We can only avoid nullpointer errors by doing this check for each button.

			else{
				if (guixy == gui[verbc_id]) {
          if (control == look_button){
            gui[verbc_id].Visible = false;
						Wait(1); 
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModeLookat); //look at item
					}
					if (control == talk_button){
						gui[verbc_id].Visible = false;
						Wait(1); 
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModeTalkto); //talk to item
					}
					if (control == interact_button){
						gui[verbc_id].Visible = false;
						Wait(1);
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModeInteract); //interact with item
					}
					if (control == custom_button1){
						gui[verbc_id].Visible = false;	// if you click elsewhere on the screen
						Wait(1);
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModeUsermode1); //custom action 1 with item
					}
					if (control == custom_button2){
						gui[verbc_id].Visible = false;	// if you click elsewhere on the screen
						Wait(1);
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModeUsermode2); //custom action 1 with item
					}
					if (control == custom_button3){
						gui[verbc_id].Visible = false;	// if you click elsewhere on the screen
						Wait(1);
						item2 = InventoryItem.GetAtScreenXY(mousex, mousey);
						item2.RunInteraction(eModePickup); //custom action 1 with item
          }
        }
      }
    }
  }
}
}
export doubleclick;