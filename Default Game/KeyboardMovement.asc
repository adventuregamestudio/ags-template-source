// Main script for module 'KeyboardMovement'

// stores current keyboard control mode (disabled by default)
KeyboardMovementMode mode = eKeyboardMovementModeNone;

// stores current walking direction of player character
KeyboardMovementDirection direction = eKeyboardMovementStop;

// define a keymap
KeyboardMovementKeymap keymap;

// track the previous key press for Tapping mode
int lastkey = 0;
bool keydown = false;

static function KeyboardMovement::SetMode(KeyboardMovementMode newmode) {
  mode = newmode;
}

// set default keys
function game_start()
{
  keymap.KeyDown = eKeyDownArrow;
  keymap.KeyLeft = eKeyLeftArrow;
  keymap.KeyRight = eKeyRightArrow;
  keymap.KeyUp = eKeyUpArrow;
  keymap.KeyDownRight = eKeyPageDown;
  keymap.KeyUpRight = eKeyPageUp;
  keymap.KeyDownLeft = eKeyEnd;
  keymap.KeyUpLeft = eKeyHome;
  keymap.KeyStop = eKeyNumPad5; 
}

function repeatedly_execute()
{
  if (mode == eKeyboardMovementModeTapping)
  {
    // if a key was pressed in tapping mode, check if it is still down
    if (lastkey > 0 && !IsKeyPressed(lastkey))
    {
      lastkey = 0;
      keydown = false;
    }
    else
    {
      keydown = true;
    }
  }

  if (IsGamePaused() || mode != eKeyboardMovementModePressing || !IsInterfaceEnabled() || player.on == 0)
  {
    return;
  }

  KeyboardMovementDirection newdirection = eKeyboardMovementStop;

  if (IsKeyPressed(keymap.KeyStop))
  {
    // always stop if the stop key is pressed
  }
  else if ((IsKeyPressed(keymap.KeyDown) && IsKeyPressed(keymap.KeyRight)) || IsKeyPressed(keymap.KeyDownRight))
  {
    newdirection = eKeyboardMovementDownRight;
  }
  else if ((IsKeyPressed(keymap.KeyUp) && IsKeyPressed(keymap.KeyRight)) || IsKeyPressed(keymap.KeyUpRight))
  {
    newdirection = eKeyboardMovementUpRight;
  }
  else if ((IsKeyPressed(keymap.KeyDown) && IsKeyPressed(keymap.KeyLeft)) || IsKeyPressed(keymap.KeyDownLeft))
  {
    newdirection = eKeyboardMovementDownLeft;
  }
  else if ((IsKeyPressed(keymap.KeyUp) && IsKeyPressed(keymap.KeyLeft)) || IsKeyPressed(keymap.KeyUpLeft))
  {
    newdirection = eKeyboardMovementUpLeft;
  }
  else if (IsKeyPressed(keymap.KeyDown))
  {
    newdirection = eKeyboardMovementDown;
  }
  else if (IsKeyPressed(keymap.KeyLeft))
  {
    newdirection = eKeyboardMovementLeft;
  }
  else if (IsKeyPressed(keymap.KeyRight))
  {
    newdirection = eKeyboardMovementRight;
  }
  else if (IsKeyPressed(keymap.KeyUp))
  {
    newdirection = eKeyboardMovementUp;
  }

  if (newdirection != direction)
  { 
    if (newdirection == eKeyboardMovementStop)
    {
      player.StopMoving();
    }
    else
    {
      int dx, dy;

      if (newdirection == eKeyboardMovementDownRight)
      {
        dx = DISTANCE;
        dy = DISTANCE;
      }
      else if (newdirection == eKeyboardMovementUpRight)
      {
        dx = DISTANCE;
        dy = -DISTANCE;
      }
      else if (newdirection == eKeyboardMovementDownLeft)
      {
        dx = -DISTANCE;
        dy = DISTANCE;
      }
      else if (newdirection == eKeyboardMovementUpLeft)
      {
        dx = -DISTANCE;
        dy = -DISTANCE;
      }
      else if (newdirection == eKeyboardMovementDown)
      {
        dx = 0;
        dy = DISTANCE;
      }
      else if (newdirection == eKeyboardMovementLeft)
      {
        dx = -DISTANCE;
        dy = 0;
      }
      else if (newdirection == eKeyboardMovementRight)
      {
        dx = DISTANCE;
        dy = 0;
      }
      else if (newdirection == eKeyboardMovementUp)
      {
        dx = 0;
        dy = -DISTANCE;
      }

      player.WalkStraight(player.x + dx, player.y + dy, eNoBlock);
    }

    direction = newdirection;
  }
}

function on_key_press(int keycode)
{ 
  if (lastkey > 0 || IsGamePaused() || mode != eKeyboardMovementModeTapping || !IsInterfaceEnabled() || player.on == 0)
  {
    return;
  }
  
  KeyboardMovementDirection newdirection;

  if (keycode == keymap.KeyDownRight)
  {
    newdirection = eKeyboardMovementDownRight;
  }
  else if (keycode == keymap.KeyUpRight)
  {
    newdirection = eKeyboardMovementUpRight;
  }
  else if (keycode == keymap.KeyDownLeft)
  {
    newdirection = eKeyboardMovementDownLeft;
  }
  else if (keycode == keymap.KeyUpLeft)
  {
    newdirection = eKeyboardMovementUpLeft;
  }
  else if (keycode == keymap.KeyDown)
  {
    newdirection = eKeyboardMovementDown;
  }
  else if (keycode == keymap.KeyLeft) 
  {
    newdirection = eKeyboardMovementLeft;
  }
  else if (keycode == keymap.KeyRight)
  {
    newdirection = eKeyboardMovementRight;
  }
  else if (keycode == keymap.KeyUp)
  {
    newdirection = eKeyboardMovementUp;
  }
  else if (keycode == keymap.KeyStop)
  {
    newdirection = eKeyboardMovementStop;
  }
  
  if (newdirection == eKeyboardMovementStop)
  {
    player.StopMoving();
    direction = newdirection;
  }
  else if (newdirection == direction)
  {
    if (keydown)
    {
      player.StopMoving();
      direction = eKeyboardMovementStop;
    }
  }
  else
  {
    int dx, dy;

    if (newdirection == eKeyboardMovementDownRight)
    {
      dx = DISTANCE;
      dy = DISTANCE;
    }
    else if (newdirection == eKeyboardMovementUpRight)
    {
      dx = DISTANCE;
      dy = -DISTANCE;
    }
    else if (newdirection == eKeyboardMovementDownLeft)
    {
      dx = -DISTANCE;
      dy = DISTANCE;
    }
    else if (newdirection == eKeyboardMovementUpLeft)
    {
      dx = -DISTANCE;
      dy = -DISTANCE;
    }
    else if (newdirection == eKeyboardMovementDown)
    {
      dx = 0;
      dy = DISTANCE;
    }
    else if (newdirection == eKeyboardMovementLeft)
    {
      dx = -DISTANCE;
      dy = 0;
    }
    else if (newdirection == eKeyboardMovementRight)
    {
      dx = DISTANCE;
      dy = 0;
    }
    else if (newdirection == eKeyboardMovementUp)
    {
      dx = 0;
      dy = -DISTANCE;
    }

    player.WalkStraight(player.x + dx, player.y + dy, eNoBlock);
    direction = newdirection;
    lastkey = keycode;
  }
}

function on_event(EventType event, int data)
{
  if (event == eEventLeaveRoom) 
  {
    direction = eKeyboardMovementStop;
  }
}
